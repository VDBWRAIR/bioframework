*** Settings ***
Library           OperatingSystem
Library           Collections

*** Variables ***
${TESTOUTPUTDIR}           ${CURDIR}/testoutput
${TESTINPUTDIR}            ${CURDIR}/testinput
${PROJDIR}                 ${CURDIR}/../
${DEFAULT_LOG_TO_CONSOLE}  True

*** Keywords ***
Compare Stripped Contents
    [Arguments]    ${contents}    ${expected_contents}
    ${stripped_contents} =    Strip String    ${contents}
    ${stripped_expected_contents} =    Strip String    ${expected_contents}
    Should Be Equal    ${stripped_contents}    ${stripped_expected_contents}

Compare Contents
    [Arguments]    ${contents}    ${expected_contents}
    Should Be Equal    ${contents}    ${expected_contents}

Verify File
    [Arguments]    ${path}    ${expected}    ${encoding}=UTF-8    ${strip}=True
    File Should Exist   ${path}
    ${content} =        Log File    ${path}    ${encoding}
    Run Keyword If      ${strip}    Compare Stripped Contents    ${content}    ${expected}
    ...                 ELSE        Compare Contents             ${content}    ${expected}

Common Teardown
    Terminate All Processes

Common Setup
    Remove Base Test Directory
    Create Base Test Directory

Create Base Test Directory
    Remove Base Test Directory
    Create Directory    ${TESTOUTPUTDIR}

Remove Base Test Directory
    Remove Directory    ${TESTOUTPUTDIR}    recursive

Directory Should Have Items
    [Arguments]    ${path}    @{expected}
    ${items} =    List Directory    ${path}
    Lists Should Be Equal    ${items}    ${expected}

Run JIP Tool And Return RC And Output
    [Arguments]       ${cmdline}    ${expected_rc}=0    ${jipdb}=${TESTOUTPUTDIR}/jip.db    ${log_to_console}=${DEFAULT_LOG_TO_CONSOLE}
    ${result} =       Run Process    ${cmdline}    shell=True    stderr=STDOUT    env:JIP_DB=${jipdb}
    Run Keyword If    ${log_to_console}    Log To Console    ${result.stdout}
    Should Be Equal As Integers     ${result.rc}                       ${expected_rc} 
    [return]    ${result.rc}    ${result.stdout}

Ensure Tool Accepts Stdin And Outputs Stdout
    [Arguments]    ${tool_and_options}    ${input_contents}    ${expected_contents}
    ${in_fastq} =                   Set Variable                ${TESTOUTPUTDIR}/input.fastq
    ${out_fastq} =                  Set Variable                ${TESTOUTPUTDIR}/output.fastq
    Create File                     ${in_fastq}                 ${input_contents}
    ${rc}    ${stdout} =            Run Jip Tool And Return RC And Output    cat ${in_fastq} | ${tool_and_options} | cat > ${out_fastq}
    Log To Console                  ${stdout}
    Should Be Equal As Integers     ${rc}                0 
    Verify File                     ${out_fastq}                ${expected_contents}

Normalized Input And Output Usage Statement
    [Arguments]    ${tool}
    ${rc}    ${stdout} =            Run JIP Tool And Return RC And Output    ${tool} --help    expected_rc=1
    Should Contain                  ${stdout}    [<input>] [-p]
    Should Contain                  ${stdout}    [<input>]
